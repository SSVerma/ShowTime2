plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def coreProps = new Properties()
def coreConfigFile = file('../core.properties')

if (coreConfigFile != null && coreConfigFile.canRead()) {
    coreConfigFile.withInputStream { coreProps.load(it) }
}

def debugProps = new Properties()
def debugConfigFile = file("../debug.properties")

if (debugConfigFile != null && debugConfigFile.canRead()) {
    debugConfigFile.withInputStream { debugProps.load(it) }
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.ssverma.showtime"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def FIELD_BASE_URL = "BASE_URL"
    def FIELD_TMDB_API_KEY = "TMDB_API_KEY"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            buildConfigField("String", FIELD_BASE_URL, debugProps.getProperty("baseUrl"))
            buildConfigField("String", FIELD_TMDB_API_KEY, coreProps.getProperty("tmdbApiKey"))
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation "androidx.activity:activity-compose:1.3.1"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.0-rc01"

    /*AAC*/
    implementation "androidx.navigation:navigation-compose:2.4.0-alpha08"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.4.0-beta01"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.compose.runtime:runtime-livedata:1.0.2"
    implementation "androidx.paging:paging-compose:1.0.0-alpha12"

    /*Compose Utils*/
    implementation "com.google.accompanist:accompanist-insets:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"

    /*Image Handling*/
    implementation "io.coil-kt:coil-compose:1.3.2"

    /*DI*/
    implementation "com.google.dagger:hilt-android:2.36"
    kapt "com.google.dagger:hilt-compiler:2.36"
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03'
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0-alpha03'

    //Networking
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.1"

    //De-sugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}