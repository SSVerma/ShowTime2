import com.ssverma.Deps
import com.ssverma.Modules
import com.ssverma.Versions

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def releaseProps = new Properties()
def releaseConfigFile = file('../release/release.properties')

def coreProps = new Properties()
def coreConfigFile = file('../core.properties')

if (coreConfigFile != null && coreConfigFile.canRead()) {
    coreConfigFile.withInputStream { coreProps.load(it) }
}

def debugProps = new Properties()
def debugConfigFile = file("../debug.properties")

if (debugConfigFile != null && debugConfigFile.canRead()) {
    debugConfigFile.withInputStream { debugProps.load(it) }
}

android {
    signingConfigs {
        releaseConfig {
            if (releaseConfigFile != null && releaseConfigFile.canRead()) {
                releaseConfigFile.withInputStream { releaseProps.load(it) }
                keyAlias releaseProps.getProperty("keyAlias")
                keyPassword releaseProps.getProperty("keyPassword")
                storeFile file(releaseProps.getProperty("keyStoreFilePath"))
                storePassword releaseProps.getProperty("keyStorePassword")
            }
        }
    }

    compileSdkVersion Versions.AndroidSdk.Compile

    defaultConfig {
        applicationId "com.ssverma.showtime"
        minSdkVersion Versions.AndroidSdk.Min
        targetSdkVersion Versions.AndroidSdk.Target
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def FIELD_TMDB_BASE_URL = "TMDB_BASE_URL"
    def FIELD_TMDB_API_READ_ACCESS_TOKEN = "TMDB_API_READ_ACCESS_TOKEN"

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.releaseConfig
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", FIELD_TMDB_BASE_URL, releaseProps.getProperty("baseUrl"))
            buildConfigField("String", FIELD_TMDB_API_READ_ACCESS_TOKEN, coreProps.getProperty("tmdbApiReadAccessToken"))
        }

        debug {
            minifyEnabled false
            buildConfigField("String", FIELD_TMDB_BASE_URL, debugProps.getProperty("baseUrl"))
            buildConfigField("String", FIELD_TMDB_API_READ_ACCESS_TOKEN, coreProps.getProperty("tmdbApiReadAccessToken"))
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion Versions.compose
    }
    packagingOptions {
        exclude 'META-INF/gradle/incremental.annotation.processors'
    }
    namespace 'com.ssverma.showtime'
}

dependencies {
    implementation project(Modules.ApiService.tmdb)
    implementation project(Modules.Core.ui)
    implementation project(Modules.Core.navigation)

    //TODO: revisit once Java module GSON progaurd issue fixed
    implementation project(Modules.Core.networking)

    implementation project(Modules.Feature.movie)
    implementation project(Modules.Feature.tv)
    implementation project(Modules.Feature.person)
    implementation project(Modules.Feature.library)
    implementation project(Modules.Feature.search)
    implementation project(Modules.Feature.auth)
    implementation project(Modules.Feature.account)

    implementation Deps.material
    implementation Deps.ComposeSupport.activity

    /*DI*/
    implementation Deps.Di.daggerHilt
    kapt Deps.Di.Kapt.daggerHiltCompiler

    /*De-sugaring*/
    coreLibraryDesugaring Deps.Desugaring.jdk

    /*Test*/
    testImplementation Deps.Test.Local.junit
    androidTestImplementation Deps.Test.Instrument.junit
}