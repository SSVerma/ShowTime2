import com.ssverma.Deps
import com.ssverma.Modules
import com.ssverma.Versions

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def coreProps = new Properties()
def coreConfigFile = file('../core.properties')

if (coreConfigFile != null && coreConfigFile.canRead()) {
    coreConfigFile.withInputStream { coreProps.load(it) }
}

def debugProps = new Properties()
def debugConfigFile = file("../debug.properties")

if (debugConfigFile != null && debugConfigFile.canRead()) {
    debugConfigFile.withInputStream { debugProps.load(it) }
}

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.ssverma.showtime"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def FIELD_BASE_URL = "BASE_URL"
    def FIELD_TMDB_API_KEY = "TMDB_API_KEY"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            buildConfigField("String", FIELD_BASE_URL, debugProps.getProperty("baseUrl"))
            buildConfigField("String", FIELD_TMDB_API_KEY, coreProps.getProperty("tmdbApiKey"))
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion Versions.compose
    }
}

dependencies {

    implementation project(Modules.Core.networking)
    implementation project(Modules.Core.paging)//Fix it, should be inside api-service-tmdb only
    implementation project(Modules.ApiService.tmdb)
    implementation project(Modules.Core.ui)
    implementation project(Modules.Shared.ui)
    implementation project(Modules.Core.domain)

    implementation Deps.core
    implementation Deps.appcompat
    implementation Deps.material

    implementation Deps.Compose.ui
    implementation Deps.Compose.material
    implementation Deps.Compose.tooling

    implementation Deps.Lifecycle.runtime
    implementation Deps.ComposeSupport.activity
    implementation Deps.ComposeSupport.constraintLayout

    /*AAC*/
    implementation Deps.ComposeSupport.navigation
    implementation Deps.Lifecycle.viewModel
    implementation Deps.ComposeSupport.paging

    /*Compose Utils*/
    implementation Deps.Accompanist.pager
    implementation Deps.Accompanist.pagerIndicators
    implementation Deps.Accompanist.navAnimation

    /*Image Handling*/
    implementation Deps.ComposeSupport.coil

    /*DI*/
    implementation Deps.Di.daggerHilt
    kapt Deps.Di.Kapt.daggerHiltCompiler

    implementation Deps.Di.hiltCompose
    kapt Deps.Di.Kapt.hiltCompiler

    /*De-sugaring*/
    coreLibraryDesugaring Deps.Desugaring.jdk

    /*Test*/
    testImplementation Deps.Test.Local.junit
    androidTestImplementation Deps.Test.Instrument.junit
}